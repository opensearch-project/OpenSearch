/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * The OpenSearch Contributors require contributions made to
 * this file be licensed under the Apache-2.0 license or a
 * compatible open source license.
 *
 * Modifications Copyright OpenSearch Contributors. See
 * GitHub history for details.
 */

/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

import org.opensearch.gradle.Version
import org.opensearch.gradle.info.BuildParams
import org.opensearch.gradle.testclusters.StandaloneRestIntegTestTask

apply plugin: 'opensearch.testclusters'
apply plugin: 'opensearch.standalone-test'
apply from : "$rootDir/gradle/bwc-test.gradle"
apply plugin: 'opensearch.rest-resources'

def configureTestTask(task, clusterName, bwcVersionStr, suite, excludeRemoteTest, extraConfig = {}) {
  task.configure {
    useCluster project, testClusters."${clusterName}"
    systemProperty 'tests.upgrade_from_version', bwcVersionStr
    systemProperty 'tests.rest.suite', suite
    nonInputProperties.systemProperty('tests.rest.cluster', "${-> testClusters."${clusterName}".allHttpSocketURI.join(",")}")
    nonInputProperties.systemProperty('tests.clustername', "${-> testClusters."${clusterName}".getName()}")

    if (excludeRemoteTest) {
      exclude '**/RemotePublicationClusterStateIT.class'
    }

    extraConfig.delegate = delegate
    extraConfig()
  }
}

for (Version bwcVersion : BuildParams.bwcVersions.wireCompatible) {
  /*
   * The goal here is to:
   * <ul>
   *  <li>start three nodes on the old version
   *  <li>run tests with systemProperty 'tests.rest.suite', 'old_cluster'
   *  <li>upgrade one node
   *  <li>run tests with systemProperty 'tests.rest.suite', 'mixed_cluster'
   *  <li>upgrade one more node
   *  <li>run tests with systemProperty 'tests.rest.suite', 'mixed_cluster' again
   *  <li>upgrade the last node
   *  <li>run tests with systemProperty 'tests.rest.suite', 'upgraded_cluster'
   * </ul>
   */
  String baseName = "v${bwcVersion}"
  String bwcVersionStr = "${bwcVersion}"

  testClusters {
    "${baseName}" {
      versions = [bwcVersionStr, project.version]
      numberOfNodes = 3
      setting 'repositories.url.allowed_urls', 'http://snapshot.test*'
      setting 'path.repo', "${buildDir}/cluster/shared/repo/${baseName}"
      setting 'http.content_type.required', 'true'
    }
  }

  tasks.register("${baseName}#oldClusterTest", StandaloneRestIntegTestTask) {
    dependsOn processTestResources
    mustRunAfter(precommit)
    configureTestTask(it, baseName, bwcVersionStr, 'old_cluster', true) {
      doFirst { delete("${buildDir}/cluster/shared/repo/${baseName}") }
    }
  }

  tasks.register("${baseName}#oneThirdUpgradedTest", StandaloneRestIntegTestTask) {
    dependsOn "${baseName}#oldClusterTest"
    configureTestTask(it, baseName, bwcVersionStr, 'mixed_cluster', true) {
      systemProperty 'tests.first_round', 'true'
      doFirst { testClusters."${baseName}".nextNodeToNextVersion() }
    }
  }

  tasks.register("${baseName}#twoThirdsUpgradedTest", StandaloneRestIntegTestTask) {
    dependsOn "${baseName}#oneThirdUpgradedTest"
    configureTestTask(it, baseName, bwcVersionStr, 'mixed_cluster', true) {
      systemProperty 'tests.first_round', 'false'
      doFirst { testClusters."${baseName}".nextNodeToNextVersion() }
    }
  }

  tasks.register("${baseName}#upgradedClusterTest", StandaloneRestIntegTestTask) {
    dependsOn "${baseName}#twoThirdsUpgradedTest"
    configureTestTask(it, baseName, bwcVersionStr, 'upgraded_cluster', true) {
      doFirst { testClusters."${baseName}".nextNodeToNextVersion() }
    }
  }

  // Remote publication cluster
  String remoteBaseName = "${baseName}-remote"

  testClusters {
    "${remoteBaseName}" {
      versions = [bwcVersionStr, project.version]
      numberOfNodes = 3
      setting 'repositories.url.allowed_urls', 'http://snapshot.test*'
      setting 'path.repo', "${buildDir}/cluster/shared/repo/${remoteBaseName}"
      setting 'http.content_type.required', 'true'
      setting 'node.attr.remote_store.routing_table.repository', 'my-fs-repository'
      setting 'node.attr.remote_store.state.repository', 'my-fs-repository'
      setting 'node.attr.remote_store.repository.my-fs-repository.type', 'fs'
      setting 'node.attr.remote_store.repository.my-fs-repository.settings.location', "${buildDir}/cluster/shared/repo/${remoteBaseName}/remote_state"
      setting 'cluster.remote_store.state.enabled', 'true'
      setting 'cluster.remote_store.publication.enabled', 'true'
    }
  }

  tasks.register("${remoteBaseName}#oldClusterTest", StandaloneRestIntegTestTask) {
    dependsOn "${baseName}#upgradedClusterTest"
    configureTestTask(it, remoteBaseName, bwcVersionStr, 'old_cluster', false) {
      doFirst { delete("${buildDir}/cluster/shared/repo/${remoteBaseName}") }
    }
  }

  tasks.register("${remoteBaseName}#oneThirdUpgradedTest", StandaloneRestIntegTestTask) {
    dependsOn "${remoteBaseName}#oldClusterTest"
    configureTestTask(it, remoteBaseName, bwcVersionStr, 'mixed_cluster', false) {
      systemProperty 'tests.first_round', 'true'
      doFirst { testClusters."${remoteBaseName}".nextNodeToNextVersion() }
    }
  }

  tasks.register("${remoteBaseName}#twoThirdsUpgradedTest", StandaloneRestIntegTestTask) {
    dependsOn "${remoteBaseName}#oneThirdUpgradedTest"
    configureTestTask(it, remoteBaseName, bwcVersionStr, 'mixed_cluster', false) {
      systemProperty 'tests.first_round', 'false'
      doFirst { testClusters."${remoteBaseName}".nextNodeToNextVersion() }
    }
  }

  tasks.register("${remoteBaseName}#upgradedClusterTest", StandaloneRestIntegTestTask) {
    dependsOn "${remoteBaseName}#twoThirdsUpgradedTest"
    configureTestTask(it, remoteBaseName, bwcVersionStr, 'upgraded_cluster', false) {
      doFirst { testClusters."${remoteBaseName}".nextNodeToNextVersion() }
    }
  }

  tasks.register(bwcTaskName(bwcVersion)) {
    dependsOn tasks.named("${remoteBaseName}#upgradedClusterTest")
  }
}
