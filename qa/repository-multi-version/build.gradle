/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * The OpenSearch Contributors require contributions made to
 * this file be licensed under the Apache-2.0 license or a
 * compatible open source license.
 *
 * Modifications Copyright OpenSearch Contributors. See
 * GitHub history for details.
 */

/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

import org.opensearch.gradle.Version
import org.opensearch.gradle.info.BuildParams
import org.opensearch.gradle.testclusters.StandaloneRestIntegTestTask

apply plugin: 'opensearch.testclusters'
apply plugin: 'opensearch.standalone-test'
apply from : "$rootDir/gradle/bwc-test.gradle"

dependencies {
  testImplementation project(':client:rest-high-level')
}

for (Version bwcVersion : BuildParams.bwcVersions.indexCompatible) {
  String baseName = "v${bwcVersion}"
  String oldClusterName = "${baseName}-old"
  String newClusterName = "${baseName}-new"

  def clusterSettings = { v ->
    return {
      version = v
      numberOfNodes = 2
      setting 'path.repo', "${buildDir}/cluster/shared/repo/${baseName}"
    }
  }

  testClusters {
    "${oldClusterName}" clusterSettings(bwcVersion.toString())
    "${newClusterName}" clusterSettings(project.version)
  }

  tasks.register("${baseName}#Step1OldClusterTest", StandaloneRestIntegTestTask) {
    useCluster project, testClusters."${oldClusterName}"
    mustRunAfter(precommit)
    doFirst {
      delete("${buildDir}/cluster/shared/repo/${baseName}")
    }
    systemProperty 'tests.rest.suite', 'step1'
  }

  tasks.register("${baseName}#Step2NewClusterTest", StandaloneRestIntegTestTask) {
    useCluster project, testClusters."${newClusterName}"
    dependsOn "${baseName}#Step1OldClusterTest"
    systemProperty 'tests.rest.suite', 'step2'
  }

  tasks.register("${baseName}#Step3OldClusterTest", StandaloneRestIntegTestTask) {
    useCluster project, testClusters."${oldClusterName}"
    dependsOn "${baseName}#Step2NewClusterTest"
    systemProperty 'tests.rest.suite', 'step3'
  }

  tasks.register("${baseName}#Step4NewClusterTest", StandaloneRestIntegTestTask) {
    useCluster project, testClusters."${newClusterName}"
    dependsOn "${baseName}#Step3OldClusterTest"
    systemProperty 'tests.rest.suite', 'step4'
  }

  tasks.register(bwcTaskName(bwcVersion)) {
    dependsOn tasks.named("${baseName}#Step4NewClusterTest")
  }

  tasks.matching { it.name.startsWith(baseName) && it.name.endsWith("ClusterTest") }.configureEach {
    it.systemProperty 'tests.old_cluster_version', bwcVersion.toString().minus("-SNAPSHOT")
    it.systemProperty 'tests.path.repo', "${buildDir}/cluster/shared/repo/${baseName}"
    def clusterName = it.name.contains("Step2") || it.name.contains("Step4") ? "${newClusterName}" : "${oldClusterName}"
    it.nonInputProperties.systemProperty('tests.rest.cluster', "${-> testClusters."${clusterName}".allHttpSocketURI.join(",")}")
    it.nonInputProperties.systemProperty('tests.clustername', "${-> testClusters."${clusterName}".getName()}")
  }
}

configurations {
  testArtifacts.extendsFrom testRuntime
  testArtifacts.extendsFrom testImplementation
}

def testJar = tasks.register("testJar", Jar) {
  archiveAppendix = 'test'
  from sourceSets.test.output
}

artifacts {
  testArtifacts testJar
}
