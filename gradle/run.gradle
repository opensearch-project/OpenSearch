/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * The OpenSearch Contributors require contributions made to
 * this file be licensed under the Apache-2.0 license or a
 * compatible open source license.
 *
 * Modifications Copyright OpenSearch Contributors. See
 * GitHub history for details.
 */

/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */
import org.opensearch.gradle.testclusters.RunTask
import org.opensearch.gradle.VersionProperties

apply plugin: 'opensearch.testclusters'

def numNodes = findProperty('numNodes') as Integer ?: 1
def numZones = findProperty('numZones') as Integer ?: 1

testClusters {
  runTask {
    testDistribution = 'archive'
    if (numZones > 1) numberOfZones = numZones
    if (numNodes > 1) numberOfNodes = numNodes
    // S3 repository configuration
    if (findProperty("enableS3")) {
      plugin(':plugins:repository-s3')
      if (findProperty("s3Endpoint")) {
        setting 's3.client.default.endpoint', findProperty("s3Endpoint")
      }
      setting 's3.client.default.region', findProperty("s3Region") ?: 'us-east-1'
      keystore 's3.client.default.access_key', findProperty("s3AccessKey") ?: System.getenv("AWS_ACCESS_KEY_ID") ?: 'test'
      keystore 's3.client.default.secret_key', findProperty("s3SecretKey") ?: System.getenv("AWS_SECRET_ACCESS_KEY") ?: 'test'


      // Remote store configuration
      setting 'node.attr.remote_store.segment.repository', 'my-s3-repo'
      setting 'node.attr.remote_store.translog.repository', 'my-s3-repo'
      setting 'node.attr.remote_store.state.repository', 'my-s3-repo'
      setting 'cluster.remote_store.state.enabled', 'true'
      setting 'node.attr.remote_store.repository.my-s3-repo.type', 's3'
      setting 'node.attr.remote_store.repository.my-s3-repo.settings.bucket', 'local-opensearch-bucket'
      setting 'node.attr.remote_store.repository.my-s3-repo.settings.base_path', 'raghraaj-local-1230'
    }
    if (findProperty("installedPlugins")) {
      installedPlugins = Eval.me(installedPlugins)

      def resolveMavenPlugin = { coords ->
        // Add default groupId if not fully qualified (less than 2 colons)
        String[] parts = coords.split(':')
        if (parts.length == 2 && parts[0].contains('.')) {
          throw new IllegalArgumentException("version is required if groupdId is specified '${coords}' Use format: groupId:artifactId:version")
        }
        String fullCoords = parts.length < 3 ? 'org.opensearch.plugin:' + coords : coords
        def config = project.configurations.detachedConfiguration(
          project.dependencies.create(fullCoords)
        )
        config.resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        plugin(project.layout.file(project.provider { config.singleFile }))
      }

      for (String p : installedPlugins) {
        // check if its a local plugin first
        if (project.findProject(':plugins:' + p) != null) {
          plugin('plugins:' + p)
        } else {
          // attempt to fetch it from maven
          project.repositories.mavenLocal()
          project.repositories {
            maven {
              name = 'OpenSearch Snapshots'
              url = 'https://central.sonatype.com/repository/maven-snapshots/'
            }
          }
          if (p.contains(':')) {
            // Maven coordinates with version specified
            String coords = p.contains('@') ? p : (p + '@zip')
            resolveMavenPlugin(coords)
          } else {
            // Not found locally, try Maven with current OS version + 0
            String version = VersionProperties.getOpenSearch().replace('-SNAPSHOT', '.0-SNAPSHOT')
            String coords = p + ':' + version + '@zip'
            resolveMavenPlugin(coords)
          }
        }
        if (p.equals("arrow-flight-rpc")) {
          // Add system properties for Netty configuration
          systemProperty 'io.netty.allocator.numDirectArenas', '1'
          systemProperty 'io.netty.noUnsafe', 'false'
          systemProperty 'io.netty.tryUnsafe', 'true'
          systemProperty 'io.netty.tryReflectionSetAccessible', 'true'
        }
      }
    }

    if (findProperty("remotePlugins")) {
      remotePlugins = Eval.me(remotePlugins)
      for (String coords : remotePlugins) {
        if (coords.startsWith('/') || coords.startsWith('file:')) {
          // Direct file path
          plugin(project.layout.file(project.provider { new File(coords) }))
        } else {
          // Maven coordinates
          def config = project.configurations.detachedConfiguration(
            project.dependencies.create(coords + '@zip')
          )
          config.resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
          project.repositories.mavenLocal()
          project.repositories {
            maven {
              name = 'OpenSearch Snapshots'
              url = 'https://central.sonatype.com/repository/maven-snapshots/'
            }
          }
          plugin(project.layout.file(project.provider { config.singleFile }))
        }
      }
    }
  }

}

tasks.register("run", RunTask) {
  useCluster testClusters.runTask;
  description = 'Runs opensearch in the foreground'
  group = 'Verification'

  impliesSubProjects = true
}
