/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * The OpenSearch Contributors require contributions made to
 * this file be licensed under the Apache-2.0 license or a
 * compatible open source license.
 *
 * Modifications Copyright OpenSearch Contributors. See
 * GitHub history for details.
 */

plugins {
  id("com.gradleup.shadow")
}

description = '''arrow-flight-core netty shaded dependencies to make it compatible with grpc-netty-shaded'''

testingConventions.enabled = false

// since we are not publishing it
generatePomFileForShadowPublication.enabled = false

dependencies {
  runtimeOnly "org.apache.arrow:flight-core:${versions.arrow}"
}

shadowJar {
  dependencies {
    include(dependency('org.apache.arrow:'))
  }
  exclude 'META-INF/maven/**'
  relocate 'io.grpc.netty', 'io.grpc.netty.shaded.io.grpc.netty'
  relocate 'io.netty', 'io.grpc.netty.shaded.io.netty'
  mergeServiceFiles()
  archiveClassifier.set("")
}

tasks.named('thirdPartyAudit').configure {
  enabled = false
}

// creating dummy javadoc to let javadoc check pass when other package use it as a dependency
tasks.register("javadocDummyFiles") {
  doFirst {
    def javadocDir = layout.buildDirectory.dir('docs/javadoc').get().asFile
    javadocDir.mkdirs()

    new File(javadocDir, "element-list").text = ""
    new File(javadocDir, "package-list").text = ""
  }
}

tasks.named('javadoc') { javadocTask ->
  enabled = false
  finalizedBy(tasks.named('javadocDummyFiles'))
}
