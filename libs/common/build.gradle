/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * The OpenSearch Contributors require contributions made to
 * this file be licensed under the Apache-2.0 license or a
 * compatible open source license.
 *
 * Modifications Copyright OpenSearch Contributors. See
 * GitHub history for details.
 */

import org.opensearch.gradle.info.BuildParams

apply plugin: 'opensearch.publish'

ext {
  // Do not fail on 'warning: using incubating module(s): jdk.incubator.vector'
  failOnJavadocWarning = false
}

base {
  archivesName = 'opensearch-common'
}

dependencies {
  // This dependency is used only by :libs:core for null-checking interop with other tools
  compileOnly "com.google.code.findbugs:jsr305:3.0.2"

  /*******
   *  !!!! NO THIRD PARTY DEPENDENCIES !!!!
   *******/

  testImplementation "com.carrotsearch.randomizedtesting:randomizedtesting-runner:${versions.randomizedrunner}"
  testImplementation "junit:junit:${versions.junit}"
  testImplementation "org.hamcrest:hamcrest:${versions.hamcrest}"

  testImplementation(project(":test:framework")) {
    exclude group: 'org.opensearch', module: 'opensearch-common'
  }
}

tasks.withType(JavaCompile).configureEach {
  options.compilerArgs -= '-Xlint:unchecked'
}

tasks.named('forbiddenApisMain').configure {
  // :libs:opensearch-common does not depend on server
  // TODO: Need to decide how we want to handle for forbidden signatures with the changes to server
  replaceSignatureFiles 'jdk-signatures'
  failOnMissingClasses = false
  ignoreSignaturesOfMissingClasses = true
}

compileJava {
  options.compilerArgs += ['--add-modules', 'jdk.incubator.vector']
  options.compilerArgs -= '-Werror' // use of incubator modules is reported as a warning
}

test {
  systemProperty 'opensearch.experimental.feature.simd.rounding.enabled', 'forced'
}

javadoc {
  options.addStringOption("-add-modules", "jdk.incubator.vector")
}
