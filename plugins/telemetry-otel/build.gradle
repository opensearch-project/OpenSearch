/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * The OpenSearch Contributors require contributions made to
 * this file be licensed under the Apache-2.0 license or a
 * compatible open source license.
 */

import org.apache.tools.ant.taskdefs.condition.Os
import org.opensearch.gradle.Architecture
import org.opensearch.gradle.OS
import org.opensearch.gradle.info.BuildParams

apply plugin: 'opensearch.internal-cluster-test'

opensearchplugin {
  description 'Opentelemetry based telemetry implementation.'
  classname 'org.opensearch.telemetry.OTelTelemetryPlugin'
  hasClientJar = false
}

dependencies {
  api project(":libs:opensearch-telemetry")
  api "io.opentelemetry:opentelemetry-api:${versions.opentelemetry}"
  api "io.opentelemetry:opentelemetry-context:${versions.opentelemetry}"
  api "io.opentelemetry:opentelemetry-sdk:${versions.opentelemetry}"
  api "io.opentelemetry:opentelemetry-sdk-common:${versions.opentelemetry}"
  api "io.opentelemetry:opentelemetry-sdk-trace:${versions.opentelemetry}"
  api "io.opentelemetry:opentelemetry-sdk-metrics:${versions.opentelemetry}"
  api "io.opentelemetry:opentelemetry-exporter-logging:${versions.opentelemetry}"
  api "io.opentelemetry:opentelemetry-exporter-logging-otlp:${versions.opentelemetry}"
  api "io.opentelemetry.semconv:opentelemetry-semconv:${versions.opentelemetrysemconv}"
  api "io.opentelemetry:opentelemetry-sdk-logs:${versions.opentelemetry}"
  api "io.opentelemetry:opentelemetry-exporter-otlp:${versions.opentelemetry}"
  api "io.opentelemetry:opentelemetry-exporter-common:${versions.opentelemetry}"
  api "io.opentelemetry:opentelemetry-exporter-otlp-common:${versions.opentelemetry}"
  runtimeOnly "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"
  runtimeOnly "com.squareup.okhttp3:okhttp:4.11.0"
  runtimeOnly "com.squareup.okio:okio-jvm:3.5.0"
  runtimeOnly "io.opentelemetry:opentelemetry-exporter-sender-okhttp:${versions.opentelemetry}"
  api "io.opentelemetry:opentelemetry-api-incubator:${versions.opentelemetry}-alpha"
  testImplementation "io.opentelemetry:opentelemetry-sdk-testing:${versions.opentelemetry}"
}


thirdPartyAudit {
  ignoreViolations(
   'io.opentelemetry.internal.shaded.jctools.queues.MpscArrayQueueConsumerIndexField',
   'io.opentelemetry.internal.shaded.jctools.queues.MpscArrayQueueProducerIndexField',
   'io.opentelemetry.internal.shaded.jctools.queues.MpscArrayQueueProducerLimitField',
   'io.opentelemetry.internal.shaded.jctools.util.UnsafeAccess',
   'io.opentelemetry.internal.shaded.jctools.util.UnsafeRefArrayAccess',
   'io.opentelemetry.exporter.internal.marshal.UnsafeAccess',
   'io.opentelemetry.exporter.internal.marshal.UnsafeAccess$UnsafeHolder'
  )

  ignoreMissingClasses(
    'android.net.http.X509TrustManagerExtensions',
    'android.net.ssl.SSLSockets',
    'android.os.Build$VERSION',
    'android.security.NetworkSecurityPolicy',
    'android.util.Log',
    'com.google.common.io.ByteStreams',
    'com.google.common.util.concurrent.ListenableFuture',
    'io.grpc.CallOptions',
    'io.grpc.Channel',
    'io.grpc.Drainable',
    'io.grpc.KnownLength',
    'io.grpc.ManagedChannel',
    'io.grpc.MethodDescriptor',
    'io.grpc.MethodDescriptor$Builder',
    'io.grpc.MethodDescriptor$Marshaller',
    'io.grpc.MethodDescriptor$MethodType',
    'io.grpc.stub.AbstractFutureStub',
    'io.grpc.stub.AbstractStub',
    'io.grpc.stub.ClientCalls',
    'org.bouncycastle.jsse.BCSSLParameters',
    'org.bouncycastle.jsse.BCSSLSocket',
    'org.conscrypt.Conscrypt',
    'org.conscrypt.Conscrypt$Version',
    'org.conscrypt.ConscryptHostnameVerifier',
    'org.openjsse.javax.net.ssl.SSLParameters',
    'org.openjsse.javax.net.ssl.SSLSocket',
    'io.opentelemetry.sdk.autoconfigure.spi.ConfigProperties',
    'io.opentelemetry.sdk.autoconfigure.spi.logs.ConfigurableLogRecordExporterProvider',
    'io.opentelemetry.sdk.autoconfigure.spi.metrics.ConfigurableMetricExporterProvider',
    'io.opentelemetry.sdk.autoconfigure.spi.traces.ConfigurableSpanExporterProvider',
    'kotlin.io.path.PathsKt',
    'io.opentelemetry.sdk.autoconfigure.spi.traces.ConfigurableSpanExporterProvider',
    'io.opentelemetry.sdk.autoconfigure.spi.internal.AutoConfigureListener'
  )
}

tasks.named("bundlePlugin").configure {
  from('config/telemetry-otel') {
    into 'config'
  }
}

tasks.register("writeTestJavaPolicy") {
  doLast {
    final File tmp = file("${buildDir}/tmp")
    if (tmp.exists() == false && tmp.mkdirs() == false) {
      throw new GradleException("failed to create temporary directory [${tmp}]")
    }
    final File javaPolicy = file("${tmp}/java.policy")
    javaPolicy.write(
      [
        "grant {",
        "  permission java.io.FilePermission \"config\", \"read\";",
        "};"
      ].join("\n"))
  }
}
